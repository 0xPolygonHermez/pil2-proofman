require "std_lookup.pil";

// With the logup protocol, it is more performant to look up to the same range;
// so we'll try to "absorb" all the introduced ranges into some of the default ranges.
// Moreover, having a fewer ranges makes the preprocessing faster and the prvoing less memory consuming

const int MAX_RANGE_LEN = (PRIME - 1) / 2;

const int OPIDS[2] = [100, 101];
int last_assigned_opid = OPIDS[length(OPIDS) - 1];

private function next_available_opid(): int {
    last_assigned_opid++;
    return last_assigned_opid;
}

private function get_opid(int min, int max, int predefined): int {
    if (predefined) {
        if (min >= 0) {
            return (max <= BYTE) ? OPIDS[0] :
                   (max <= TWOBYTES) ? OPIDS[1] :
                   next_available_opid();
        }
    }
    else if (min == 0) {
        return (max == BYTE) ? OPIDS[0] :
               (max == TWOBYTES) ? OPIDS[1] :
               next_available_opid();
    }

    return next_available_opid();
}

const int BYTE = 2**8-1;
const int TWOBYTES = 2**16-1;

airtemplate U8Air(const int N = 2**8) {
    if (N != 2**8) {
        error(`The number of rows N should be 2**8 to use the predefined range U8, got N=${N} instead`);
    }

    // save the airgroup id and air id of the table for latter use
    proof.std.u8.airgroup_id = AIRGROUP_ID;
    proof.std.u8.air_id = AIR_ID;

    col witness mul;

    @u8air{reference: mul};

    col fixed U8 = [0..BYTE];
    lookup_proves(OPIDS[0], [U8], mul, PIOP_NAME_RANGE_CHECK);
}

airtemplate U16Air(const int N = 2**16) {
    if (N != 2**16) {
        error(`The number of rows N should be 2**16 to use the predefined range U16, got N=${N} instead`);
    }

    // save the airgroup id and air id of the table for latter use
    proof.std.u16.airgroup_id = AIRGROUP_ID;
    proof.std.u16.air_id = AIR_ID;

    col witness mul;

    @u16air{reference: mul};

    col fixed U16 = [0..TWOBYTES];
    lookup_proves(OPIDS[1], [U16], mul, PIOP_NAME_RANGE_CHECK);
}

airtemplate SpecifiedRanges(const int N, const int opids[], const int opids_count, const int predefineds[], const int mins[], const int maxs[]) {
    // save the airgroup id and air id of the table for latter use
    proof.std.specified.airgroup_id = AIRGROUP_ID;
    proof.std.specified.air_id = AIR_ID;

    @specified_ranges{num_rows: N};

    col witness mul[opids_count];

    for (int j = 0; j < opids_count; j++) {
        int opid = opids[j];
        int predefined = predefineds[j];
        int min = mins[j];
        int max = maxs[j];

        @specified_ranges{reference: mul[j], predefined: predefined, min: min, max: max, min_neg: min < 0, max_neg: max < 0};

        if (N < max - min + 1) {
            error(`The range [min,max]=[${min},${max}] is too big, the maximum range length is ${N}`);
        }

        col fixed RANGE = [min..max-1,max...];
        lookup_proves(opid, [RANGE], mul[j], PIOP_NAME_RANGE_CHECK);
    }
}

// Three user interfaces

// 1] Interface where the user directly calls the range check over a column and a range

/**
 * TODO: Add description
 */
function range_check(expr colu, int min, int max, expr sel = 1, int predefined = 1) {
    range_validator(min, max);

    @range_def{predefined: predefined, min: min, max: max, min_neg: min < 0, max_neg: max < 0};

    if (min < 0) {
        println(`The provided min=${min} is negative. Falling back to specified range...`);
    } else if (max > TWOBYTES) {
        println(`The provided max=${max} is greater than the maximum predefined ${TWOBYTES}. Falling back to specified range...`);
    }

    const int opid = opid_process(min, max, predefined);

    // Check if the range can be absorbed into the predefined ranges
    const int absorb = predefined && min >= 0 && max <= TWOBYTES;

    // Define the assume
    if (absorb) {
        if (min == 0 && (max == BYTE || max == TWOBYTES)) {
            const int is_u8 = max == BYTE ? 1 : 0;

            proof.std.rc.u8_used = proof.std.rc.u8_used || is_u8;
            proof.std.rc.u16_used = proof.std.rc.u16_used || 1-is_u8;
            lookup_assumes(opid, [colu], sel, PIOP_NAME_RANGE_CHECK);
        } else {
            // Here, we need to reuse to some of the default ranges depending
            // on the values of min and max
            if (max <= BYTE) {
                // reuse U8
                proof.std.rc.u8_used = 1;

                // first prove that colu - min is in U8
                lookup_assumes(opid, [colu - min], sel, PIOP_NAME_RANGE_CHECK);

                // then prove that max - colu is in U8
                lookup_assumes(opid, [max - colu], sel, PIOP_NAME_RANGE_CHECK);
            } else if (max <= TWOBYTES) {
                // reuse U16
                proof.std.rc.u16_used = 1;

                // first prove that colu - min is in U16
                lookup_assumes(opid, [colu - min], sel, PIOP_NAME_RANGE_CHECK);

                // second prove that max - colu is in U16
                lookup_assumes(opid, [max - colu], sel, PIOP_NAME_RANGE_CHECK);
            }
        }
    } else {
        lookup_assumes(opid, [colu], sel, PIOP_NAME_RANGE_CHECK);
    }

    // Define the prove
    define_proves(absorb);
}

/**
 * TODO: Add description
 */
function range_check_group(const expr cols[], const int min, const int max, const expr sels[], const int predefined = 1) {
    for (int i = 0; i < length(cols); i = i + 1) {
        range_check(cols[i], min, max, sels[i], predefined);
    }
}

// 2] Interface where the user can perform a range check over two ranges based on a selector

/**
 * TODO: Add description
 * TODO: Generalize to more than 2 ranges
 * If range_sel = 1, then the range [min1,max1] is checked, otherwise [min2,max2]
 */
function multi_range_check(expr colu, int min1, int max1, int min2, int max2, expr range_sel = 1, expr sel = 1, int predefined = 0): int {
    if (predefined) {
        error(`WIP: Multi range check cannot be predefined`);
    }

    if (min1 == min2 && max1 == max2) {
        error(`The ranges defined in the multi range check are the same: [${min1},${max1}]`);
    }

    range_validator(min1, max1);
    range_validator(min2, max2);

    @range_def{predefined: predefined, min: min1, max: max1, min_neg: min1 < 0 , max_neg: max1 < 0};
    @range_def{predefined: predefined, min: min2, max: max2, min_neg: min2 < 0 , max_neg: max2 < 0};

    const int opid1 = opid_process(min1, max1, predefined);
    const int opid2 = opid_process(min2, max2, predefined);

    // Define the assume
    lookup_assumes_dynamic([opid1,opid2], range_sel*(opid1-opid2) + opid2, [colu], sel, PIOP_NAME_RANGE_CHECK);

    // Define the prove
    define_proves(0);

    // Note: This solution improves the naive solution:
    //   · range_check(colu, min1, max1, 0, sel*range_sel).
    //   · range_check(colu, min2, max2, 0, sel*(1-range_sel)).
    // by just performing 1 assume and 2 proves instead
    //
    // Q: Can I do it with 1 assume and 1 prove? E.g.:
    // lookup_assumes_dynamic([opid1,opid2], range_sel*(opid1-opid2) - opid2, [colu], sel, PIOP_NAME_RANGE_CHECK);
    // lookup_proves(opid, [min1..max1-1,max1...]U[min2..max2-1,max2...], mul, PIOP_NAME_RANGE_CHECK);
}

// 3] Interface where the user can first get the range check id:
//      int id_range_24 = range_check_id(0, 2**24-1);
//      int id_range_16 = range_check_id(0, 2**16-1);
//      int id_range_15 = range_check_id(0, 2**15-1);
//    and then perform the range check using a user-defined range selector:
//      col witness sel_8, sel_16, sel_32;
//      range_check_dynamic(colu, id_range_24 * sel_8 + id_range_16 * sel_16 + id_range_15 * sel_32);

function range_check_id(const int min, const int max, const int predefined = 0): int {
    if (predefined) {
        error(`WIP: Range check id cannot be predefined`);
    }

    range_validator(min, max);

    @range_def{predefined: predefined, min: min, max: max, min_neg: min < 0, max_neg: max < 0};

    container air.std.rcid alias rcid {
        int opids_count_id = 0;
        int opids_id[100];
        int called = 0;
    }

    const int opid = opid_process(min, max, predefined);

    // Save the opid for use in the dynamic range check
    rcid.opids_id[rcid.opids_count_id] = opid;
    rcid.opids_count_id++;

    // At the end of the air, check if dynamic range checks were called
    on final air check_dynamic_called();

    return opid;
}

function range_check_dynamic(const expr colu, const expr range_sel, const expr sel = 1) {
    use air.std.rcid;

    // Mark the dynamic range check as called
    called = 1;

    int opids[opids_count_id];
    for (int i = 0; i < opids_count_id; i++) {
        opids[i] = opids_id[i];
    }

    // Define the assume
    lookup_assumes_dynamic(opids, range_sel, [colu], sel, PIOP_NAME_RANGE_CHECK);

    // Define the prove
    define_proves(0);
}

// Private functions
private function check_dynamic_called() {
    use air.std.rcid;
    if (!called) {
        error(`Dynamic range check was not called`);
    }
}

private function range_validator(const int min, const int max) {
    if (min > max) {
        error(`min = ${min} cannot be greater than max = ${max}`);
    } else if (min == max) {
        error(`Min and max are the same, you should use a single value check instead: ${colu} == ${min}`);
    } else if (max - min + 1 < 8) { // The 8 was chosen comparing the cost of logup vs the cost of a multiple value check
        const int diff = max - min + 1;

        string diff_str = `(${colu} - ${min})`;
        for (int i = 1; i < diff; i = i + 1) {
            diff_str = diff_str + `·(${colu} - ${min + i})`;
        }

        println(`The range [${min}..${max}] is too small, consider using a multiple value check instead: ${diff_str} == 0`);
    } else if (max - min + 1 > MAX_RANGE_LEN) {
        error(`The range [${min}..${max}] is too big, the maximum range length is (PRIME - 1) / 2 = ${MAX_RANGE_LEN}`);
    } else if (max >= PRIME) {
        error(`The maximum value of the range cannot be greater than the field order ${PRIME}`);
    } else if (min <= -PRIME) {
        error(`The minimum value of the range cannot be less than the negative field order ${-PRIME}`);
    }
}

private function opid_process(const int min, const int max, const int predefined): int {
    container proof.std.rc alias rcproof {
        int max_pre_airgroup_id = 0;
        int max_airgroup_id = 0;
        int opids_count = 0;
        int u8_used = 0;
        int u16_used = 0;
        int specified_N = 0;

        // FIX: dynamic arrays not ready
        int opids[100];
        int predefineds[100];
        int mins[100];
        int maxs[100];
    }

    // If the range has already been introduced, we reuse the same opid
    for (int i = 0; i < rcproof.opids_count; i++) {
        if (predefined == rcproof.predefineds[i] && min == rcproof.mins[i] && max == rcproof.maxs[i]) {
            return rcproof.opids[i];
        }
    }

    // Otherwise, we get a new opid
    const int opid = get_opid(min, max, predefined);

    // Save range data for later use
    rcproof.predefineds[rcproof.opids_count] = predefined;
    rcproof.mins[rcproof.opids_count] = min;
    rcproof.maxs[rcproof.opids_count] = max;
    rcproof.opids[rcproof.opids_count] = opid;
    rcproof.opids_count++;

    // if the opid is not predefined and the range is bigger than the current specified_N, we update it
    if (opid > OPIDS[1] && max - min > rcproof.specified_N) {
        rcproof.specified_N = max - min;
    }

    return opid;
}

private function define_proves(const int absorb) {
    if (absorb) {
        // Update the max airgroup id
        if (proof.std.rc.max_pre_airgroup_id < AIRGROUP_ID) {
            proof.std.rc.max_pre_airgroup_id = AIRGROUP_ID;
        }

        on final airgroup declarePreRangeAir();
        on final proof createPreMetadata();
    } else {
        // Update the max airgroup id
        if (proof.std.rc.max_airgroup_id < AIRGROUP_ID) {
            proof.std.rc.max_airgroup_id = AIRGROUP_ID;
        }

        on final airgroup declareRangeAir();
        on final proof createMetadata();
    }
}

private function declarePreRangeAir() {
    use proof.std.rc;

    // The U8 and U16 airs are only needed once, so we wait for the last airgroup that uses them
    if (AIRGROUP_ID != max_pre_airgroup_id) {
        return;
    }

    if (u8_used) {
        // container used to store the airgroup id and air id of the table
        container proof.std.u8 {
            int airgroup_id = 0;
            int air_id = 0;
        }

        // TODO: The following airtemplate should be instantiated outside of the airgroup
        airgroup U8Air {
            U8Air();
        }
    }

    if (u16_used) {
        container proof.std.u16 {
            int airgroup_id = 0;
            int air_id = 0;
        }

        // TODO: The following airtemplate should be instantiated outside of the airgroup
        airgroup U16Air {
            U16Air();
        }
    }
}

private function declareRangeAir() {
    container proof.std.specified {
        int airgroup_id = 0;
        int air_id = 0;
    }

    use proof.std.rc;

    // The specified ranges air is only needed once, so we wait for the last airgroup that uses them
    if (AIRGROUP_ID != max_airgroup_id) {
        return;
    }

    const int next_pow2 = 2**(log2(specified_N));

    airgroup SpecifiedRanges {
        SpecifiedRanges(next_pow2, opids, opids_count, predefineds, mins, maxs);
    }
}

private function createPreMetadata() {
    if (proof.std.rc.u8_used) {
        @u8air{airgroup_id: proof.std.u8.airgroup_id, air_id: proof.std.u8.air_id};
    }

    if (proof.std.rc.u16_used) {
        @u16air{airgroup_id: proof.std.u16.airgroup_id, air_id: proof.std.u16.air_id};
    }
}

private function createMetadata() {
    @specified_ranges{airgroup_id: proof.std.specified.airgroup_id, air_id: proof.std.specified.air_id};
}
